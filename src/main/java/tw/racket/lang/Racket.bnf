{
  parserClass="tw.racket.lang.parser.RacketParser"
  parserUtilClass="tw.racket.lang.parser.RacketParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Racket"
  psiImplClassSuffix="Impl"
  psiPackage="tw.racket.lang.psi"
  psiImplPackage="tw.racket.lang.psi.impl"

  elementTypeHolderClass="tw.racket.lang.psi.RacketTypes"
  elementTypeClass="tw.racket.lang.psi.RacketElementType"
  tokenTypeClass="tw.racket.lang.psi.RacketTokenType"

  psiImplUtilClass="tw.racket.lang.psi.impl.RacketPsiImplUtil"
}

racketFile ::= LANG body

body ::= item_*

private item_ ::= COMMENT | form
form ::= <<parenthesis (DEFINE IDENTIFIER expression)>>
         <<parenthesis (DEFINE <<parenthesis IDENTIFIER*>> form*)>>
         | expression

keyword ::= QUOTE | LAMBDA | IF | SET | BEGIN | COND | AND
                    | OR | CASE | LET | LET_STAR | LET_REC | DO | DELAY
                    | QUASIQUOTE | ELSE | DEFINE | UNQUOTE | UNQUOTE_SPLICING
expression ::= literal
               | <<parenthesis (IF expression expression expression)>>
               // function call
               | <<parenthesis (IDENTIFIER expression*)>>
private boolean_ ::= TRUE | FALSE
literal ::= quotation
          | boolean_
          | CHAR_LITERAL
          | STRING_LITERAL
          | NUMBER_LITERAL
private meta parenthesis ::= LPAREN <<p>> RPAREN
                       | LBRACE <<p>> RBRACE
                       | LBRACK <<p>> RBRACK
any ::= keyword | literal | datum | form
private simple_datum ::= boolean_
                       | NUMBER_LITERAL
                       | CHAR_LITERAL
                       | STRING_LITERAL
                       | VARIABLE
private compound_datum ::= list | vector
list ::= <<parenthesis datum*>>
       | abbreviation
abbreviation ::= (SINGLE_QUOTE | ABBREVIATION_PREFIX) datum
vector ::= HASH_LPAREN datum* RPAREN
         | HASH_LBRACE datum* RBRACE
         | HASH_LBRACK datum* RBRACK
private datum ::= simple_datum | compound_datum
quotation ::= SINGLE_QUOTE datum
            | SINGLE_QUASIQUOTE datum
            | <<parenthesis (QUOTE datum)>>
